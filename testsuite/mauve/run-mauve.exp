load_lib dg.exp
load_lib openldk.exp

dg-init

set dg-do-what-default run

set testargs ""

proc openldk_load { classfile } {

    global testargs
    set timeout 5
    verbose "load $classfile"
    set classname [string map {".class" ""} $classfile]
    set result [local_exec "../openldk mauve.openldkHarness $testargs" "" "" $timeout]
    verbose "result = $result"
    set status [lindex $result 0]
    set output [lindex $result 1]

    # Check if the test is in the expected failures file
    if {[is_expected_failure $classname]} {
        if { $status == 0 } {
            # Test passed but was expected to fail (xpass)
            return [list "xpass" $output]
        } else {
            # Test failed as expected (xfail)
            return [list "xfail" $output]
        }
    } else {
        if { $status == 0 } {
            # Test passed as expected (pass)
            return [list "pass" $output]
        } else {
            # Test failed unexpectedly (fail)
            return [list "fail" $output]
        }
    }
}

proc openldk-dg-test { prog do_what extra_tool_flags } {

    global testargs
    set timeout 5

    verbose "prog $prog" 2
    verbose "do_what $do_what" 2
    verbose "extra_tool_flags $extra_tool_flags" 2

    # Run the javac command
    set JAVA_HOME [getenv "JAVA_HOME"]
    set status [local_exec "$JAVA_HOME/../bin/javac $prog" "" "" $timeout]

    verbose "STATUS = $status"

    set classname [string map {".java" ""} $prog]

    set testargs [string map {"/" "."} [string map {"mauve/" ""} $classname]]

    return [list [lindex $status 1] "$classname.class"]
}

# Use find to locate all .java files under the specified directory
# and grep to filter those that contain "implements Testlet"
set tlist [exec find mauve/gnu/testlet/java -name "*.java" -type f | xargs grep -l "implements Testlet"]

# Convert the result into a Tcl list
set tlist [split $tlist "\n"]

# Optionally, sort the list if needed
set tlist [lsort $tlist]

set ldkcp [getenv "LDK_CLASSPATH"]
setenv "LDK_CLASSPATH" "$ldkcp:.:./mauve"
setenv "CLASSPATH" "$ldkcp:.:./mauve"

set JAVA_HOME [getenv "JAVA_HOME"]
local_exec "$JAVA_HOME/../bin/javac mauve/openldkHarness.java" "" "" "10"

foreach test $tlist {

 		verbose "Testing $test"
		dg-test $test "" ""

}

dg-finish
